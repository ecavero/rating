import groovy.xml.*

apply plugin: "groovy"


repositories {
	mavenCentral()
	maven { url "http://java.freehep.org/maven2" } /* Aquí está el jnlp*/
}


dependencies {
	groovy "org.codehaus.groovy:groovy-all:1.8.4"
	compile "javax.jnlp:jnlp:1.2"
}

jar {
	manifest {
		attributes "Main-Class": "com.ecavero.rating.app.Main", "Class-Path": configurations.runtime.collect { "lib/${it.name}"}.join(" ")
	}
}


def copiarLib() {
	configurations.runtime.each {
		ant.copy(file: it.path, todir: "build/libs/lib")
	}
}

def crearXml() {
	def sw = new StringWriter()
	def xml = new MarkupBuilder(sw)
	def titulo = "Sistema"
	def ruta = new File("build/libs/").canonicalPath
	xml.jnlp(spec: "1.6+", codebase: "http://localhost:8080/rating-web", href: "rating.jnlp") {
		information() {
			title(titulo)
			vendor("Daniel Rodríguez")
			homepage(href: "ayuda.html")
			description(titulo)
			description(kind: "short") {
				mkp.yield "Sistema"
			}
			icon(href: "imagenes/icono.jpg")
			"offline-allowed"()
			shortcut() {
				desktop()
				menu(submenu: "Ratings")
			}
		}
		
		security {
			"all-permissions"()
		}
		
		resources() {
			j2se(version: "1.6+")
			jar(href: "rating-app.jar")
			configurations.runtime.each{
				jar(href: "lib/${it.name}")
			}
		}
		"application-desc"("main-class": "com.ecavero.rating.app.Main")
	}
	new File("rating-web/src/main/webapp/rating.jnlp").text = sw	
}

def firmarJarras() {
	ant.signjar(jar: "build/libs/rating-app.jar", alias: "prueba", storepass: "231248023158")
	configurations.runtime.each {
		ant.signjar(jar: "${it.path}", alias: "prueba", storepass: "231248023158")
	}
}


build.doLast {
	firmarJarras()
	copiarLib()
	crearXml()
}


